pipeline {
  agent none
  parameters {
    string(
      name: 'workspace',
      defaultValue: 'oo',
      description: 'Custom workspace dir'
    )
    string(
      name: 'branch',
      description: 'Branch name'
    )
  }
  stages {
    stage('Clean workspace') {
      when {
        expression { !params.branch.isEmpty() }
      }
      steps { script {
        currentBuild.displayName += ' ' + params.branch
        parallel cleanStages()
      } }
    }
  }
}

def cleanStages() {
  Map nodes = [:]
  String customDir = params.workspace
  String branchDir = params.branch.replaceAll(/\//,'_')

  nodesByLabel("windows").each {
    nodes["${it}"] = { ->
      node(it) { stage("${it}") {
        powershell "Remove-Item -Recurse -Force C:\\${customDir}\\${branchDir}*"
      } }
    }
  }

  nodesByLabel("linux").each {
    nodes["${it}"] = { ->
      node(it) { stage("${it}") {
        sh "rm -rf /home/jenkins/workspace/oo_${branchDir}"
      } }
    }
  }

  return nodes
}
